# Author: mosra <mosra@centrum.cz>
pkgname=corrade
pkgver=dev.gcc47
pkgrel=1
pkgdesc="C++11/C++14 multiplatform utility library (built with GCC 4.7)"
arch=('i686' 'x86_64')
url="http://magnum.graphics/corrade/"
license=('MIT')
makedepends=('cmake' 'ninja' 'gcc47')
options=(!strip)
provides=('corrade-git')

_rootdir=$startdir/../../

build() {
    # Disable flags unknown to GCC 4.7
    newcxxflags=$(echo $CXXFLAGS | sed s/-fstack-protector-strong.//g)
    export CXXFLAGS="$newcxxflags"

    if [ ! -d "$_rootdir/build-gcc47" ] ; then
        mkdir "$_rootdir/build-gcc47"
        cd "$_rootdir/build-gcc47"

        cmake .. \
            -DCMAKE_CXX_COMPILER=g++-4.7 \
            -G Ninja
    fi

    cd "$_rootdir/build-gcc47"

    cmake .. \
        -DCMAKE_BUILD_TYPE=Debug \
        -DCMAKE_INSTALL_PREFIX=/usr \
        -DBUILD_TESTS=ON
    ninja
}

check() {
    cd "$_rootdir/build-gcc47"
    CORRADE_TEST_COLOR=ON ctest --output-on-failure
}

package() {
    cd "$_rootdir/build-gcc47"
    DESTDIR="$pkgdir/" ninja install
}
